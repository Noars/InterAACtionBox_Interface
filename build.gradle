
buildscript {
    group = 'com.github.InteraactionGroup'
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'application'
    id 'distribution'
    id 'de.undercouch.download' version '3.2.0'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'io.freefair.lombok' version '4.1.4'
    id 'com.github.spotbugs' version '2.0.0'
    id 'net.researchgate.release' version '2.6.0'
    id 'com.github.hierynomus.license-report' version '0.15.0'
}

ext.javafxVersion = "11.0.2"

// Make it available everywhere via the project ext:
ext {
    Download = de.undercouch.gradle.tasks.download.Download
    download = download
}

javafx {
    version = javafxVersion
    modules = ['javafx.controls', 'javafx.swing', 'javafx.media', 'javafx.web']
}

apply plugin: 'java'
apply plugin: 'distribution'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'com.github.spotbugs'
apply plugin: 'distribution'

apply from: "${rootDir}/gradle/package.gradle"
apply from: "${rootDir}/gradle/jre.gradle"
apply from: "${rootDir}/gradle/drivers.gradle"
apply from: "${rootDir}/gradle/installer.gradle"

sourceSets {
    main {
        resources {
            srcDirs "src/resources"
        }
    }
}

spotbugs {
    toolVersion = '4.0.0-beta3'

    //ignoreFailures = false
    //effort = "max"
    //reportLevel = "high"
}

mainClassName = 'main.Main'

jar {
    manifest {
        attributes 'Main-Class': 'main.Main'
    }
}

allprojects {
    group = 'com.github.gazePlay'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()

        maven {
            url = 'https://jitpack.io'
        }
    }
}

dependencies {
    compile 'com.github.GazePlay:TobiiStreamEngineForJava:5.0'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'org.rxtx:rxtx:2.1.7'
    compile 'ch.qos.logback:logback-classic:1.2.1'
    compile 'ch.qos.logback:logback-core:1.2.1'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'uk.org.lidalia:sysout-over-slf4j:1.0.2'
    compile 'javax.media:jmf:2.1.1e'
    compile 'commons-io:commons-io:2.5'
    implementation 'org.json:json:20090211'
    compile 'ws.schild:jave-all-deps:2.5.0'
    compile 'ws.schild:jave-core:2.5.0'
    runtime 'org.slf4j:jcl-over-slf4j:1.7.25'
    runtime 'org.slf4j:log4j-over-slf4j:1.7.25'
    implementation group: 'com.sun.mail', name: 'jakarta.mail', version: '2.0.1'
    implementation group: 'jakarta.activation', name: 'jakarta.activation-api', version: '2.1.0'
}


distributions {
    noJRE {
        contents {
            with distributions.main.contents
            from("build/reports")
            from("build/bin") {
                into 'bin'
            }
        }
    }

    windows {
        contents {
            with distributions.main.contents
            from("build/reports")
            from("bin/interaactionBoxOS-windows.bat") {
                into 'bin'
            }
            from("scriptsWindows") {
                into 'lib/scriptsWindows'
            }
            from("build/jre/windows-x64") {
                into 'lib/jre'
            }
            from("python") {
                into 'lib/python'
            }
            from("wget") {
                into 'lib/wget'
            }
            from("GoogleChromePortable/App") {
                into 'lib/google/App'
            }
            from("GoogleChromePortable/Other") {
                into 'lib/google/Other'
            }
            from("GoogleChromePortable/GoogleChromePortable.exe") {
                into 'lib/google/'
            }
            from("GoogleChromePortable/Data.zip") {
                into 'lib/google/'
            }
        }
    }

    linux {
        contents {
            with distributions.main.contents
            from("build/reports")
            from("bin/interaactionBoxOS-linux.sh") {
                into 'bin'
            }
            from("build/jre/linux-x64") {
                into 'lib/jre'
            }
            from("build/tobiiDrivers") {
                into 'tobiiDrivers'
            }
            from("scripts") {
                into 'bin/scripts'
            }
            from("images") {
                into 'bin/images'
            }
        }
    }
}

tasks.jar.dependsOn('createLicence')

task createLicence(dependsOn: ['processResources'], type: Copy, group: 'license') {
    from("${rootDir}/gradle/templates/licence.txt")
    expand([
            "date"  : new Date().format('yyyy-MM-dd')
    ])
    into("${processResources.destinationDir}/data/common")
}

task('downloadAndExtractLinuxDependecies').dependsOn('downloadAndExtractLinuxJREwithLibavPlugin', 'downloadAndExtractTobiiDriversForLinux')

windowsDistZip.dependsOn(downloadAndExtractWindowsJRE)
linuxDistTar.dependsOn(downloadAndExtractLinuxDependecies)

tasks.withType(Tar){
    compression = Compression.GZIP
    archiveExtension.set('tar.gz')
}

distZip {
    duplicatesStrategy = 'exclude'
}

tasks.withType(Tar){
    compression = Compression.GZIP
    archiveExtension.set('tar.gz')
}

defaultTasks 'clean', 'build'
startScripts.enabled = false

afterReleaseBuild.dependsOn generateWindowsInstallerInDocker