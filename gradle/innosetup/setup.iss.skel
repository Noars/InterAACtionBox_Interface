; InteraactionBoxInterface Inno Setup Skeleton Script
;
; PLEASE NOTE:
;
; 1. This script is a SKELETON and is meant to be parsed by the Gradle
;    task "innosetup" before handing it to the Inno Setup compiler (ISCC)
;
; 2. All VARIABLES with a dollar sign and curly brackets are replaced
;    by Gradle, e.g. "applicationVersion" below
;
; 3. The script is COPIED to build/innosetup before its run,
;    so all relative paths refer to this path!
;
; 4. All BACKSLASHES must be escaped
;

[Setup]
AppId=InterAACtionBoxAFSR
AppName=InterAACtionBoxAFSR
AppVersion=${applicationVersion}
AppVerName=InterAACtionBoxAFSR ${applicationVersion}
AppPublisher=Univ. Grenoble Alpes - LIG - GETALP
AppPublisherURL=https://github.com/InteraactionGroup/InterAACtionBox_Interface
AppSupportURL=https://github.com/InteraactionGroup/InterAACtionBox_Interface/issues
AppUpdatesURL=https://github.com/InteraactionGroup/InterAACtionBox_Interface/releases

SetupIconFile=interface.ico
WizardStyle=modern
WizardImageFile=interface.bmp
WizardSmallImageFile=interface.bmp

DefaultDirName={commonpf}\\InterAACtionBoxAFSR
DefaultGroupName=InterAACtionBoxAFSR
LicenseFile=licence.txt

OutputDir=..\\distributions
OutputBaseFilename=InterAACtionBoxAFSR-windows-x64-installer
;Compression=lzma
SolidCompression=yes

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"

[Files]
Source: "interface.ico"; DestDir: "{app}\\bin"; Flags: ignoreversion
Source: "..\\${unpackedDirectory}\\bin\\*"; DestDir: "{app}\\bin"; Flags: ignoreversion
Source: "..\\${unpackedDirectory}\\lib\\*"; DestDir: "{app}\\lib"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "licence.txt"; DestDir: "{app}\\license"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\\InterAACtionBoxAFSR"; Filename: "{app}\\bin\\interaactionBoxOS-windows.bat"; IconFilename: "{app}\\bin\\interface.ico"; Flags: runminimized
Name: "{group}\\Uninstall InterAACtionBoxAFSR"; Filename: "{uninstallexe}"; IconFilename: "{app}\\bin\\gazeicon.ico"
Name: "{commondesktop}\\InterAACtionBoxAFSR"; Filename: "{app}\\bin\\interaactionBoxOS-windows.bat"; Tasks: desktopicon; IconFilename: "{app}\\bin\\interface.ico"

[Run]
Filename: "{app}\\bin\\interaactionBoxOS-windows.bat"; Description: "{cm:LaunchProgram,InterAACtionBoxAFSR}"; Flags: shellexec postinstall skipifsilent

[Code]
function GetUninstallString: string;
var
  sUnInstPath: string;
  sUnInstallString: String;
begin
  Result := '';
  sUnInstPath := ExpandConstant('Software\\Microsoft\\Windows\\CurrentVersion\\Uninstall\\InterAACtionBoxAFSR_is1'); { Your App GUID/ID }
  sUnInstallString := '';
  if not RegQueryStringValue(HKLM, sUnInstPath, 'UninstallString', sUnInstallString) then
    RegQueryStringValue(HKCU, sUnInstPath, 'UninstallString', sUnInstallString);
  Result := sUnInstallString;
end;

function IsUpgrade: Boolean;
begin
  Result := (GetUninstallString() <> '');
end;

function InitializeSetup: Boolean;
var
  V: Integer;
  iResultCode: Integer;
  sUnInstallString: string;
begin
  Result := True; { in case when no previous version is found }
  if RegValueExists(HKEY_LOCAL_MACHINE,'Software\\Microsoft\\Windows\\CurrentVersion\\Uninstall\\InterAACtionBoxAFSR_is1', 'UninstallString') then  { Your App GUID/ID }
  begin
    V := MsgBox(ExpandConstant('An older version of InteraactionBoxInterface was detected. Do you want to uninstall it?'), mbInformation, MB_YESNO); { Custom Message if App installed }
    if V = IDYES then
    begin
      sUnInstallString := GetUninstallString();
      sUnInstallString :=  RemoveQuotes(sUnInstallString);
      Exec(ExpandConstant(sUnInstallString), '', '', SW_SHOW, ewWaitUntilTerminated, iResultCode);
      Result := True; { if you want to proceed after uninstall }
      { Exit; //if you want to quit after uninstall }
    end
    else
      Result := False; { when older version present and not uninstalled }
  end;
end;